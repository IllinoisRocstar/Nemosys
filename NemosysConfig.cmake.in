@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(jsoncons)
find_dependency(Omega_h)
find_dependency(VTK 8.2.0)
find_dependency(Eigen3)

if(@ENABLE_OPENCASCADE@)  # ENABLE_OPENCASCADE
  find_dependency(OpenCASCADE)
endif()

if(@ENABLE_HDF5@)  # ENABLE_HDF5
  if(WIN32)
    find_dependency(hdf5 COMPONENTS CXX CXX_HL CONFIG)
  else()
    find_dependency(HDF5 COMPONENTS CXX)
  endif()
endif()

if(@ENABLE_NETGEN@ AND @Netgen_FOUND@)  # ENABLE_NETGEN AND Netgen_FOUND
  find_dependency(Netgen)
endif()

if(@ENABLE_OPENMP@)  # ENABLE_OPENMP
  find_dependency(OpenMP)
endif()

if(@ENABLE_CONSRV_SURFACE_TRANSFER@)  # ENABLE_CONSRV_SURFACE_TRANSFER
  find_dependency(IMPACT)
endif()

set(NEM_PREV_CMAKE_MODULE_PATH_ ${CMAKE_MODULE_PATH})
list(INSERT CMAKE_MODULE_PATH 0 ${CMAKE_CURRENT_LIST_DIR})

if(@ENABLE_GMSH@)  # ENABLE_GMSH
  find_dependency(Gmsh)
endif()

set(CMAKE_MODULE_PATH ${NEM_PREV_CMAKE_MODULE_PATH_})
unset(NEM_PREV_CMAKE_MODULE_PATH_)

include("${CMAKE_CURRENT_LIST_DIR}/Nemosys.cmake")
check_required_components(Nemosys)
